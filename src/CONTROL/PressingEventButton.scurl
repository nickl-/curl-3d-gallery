||| Copyright (C) 1998-2008, Sumisho Computer Systems Corp. All Rights Reserved.

||| Licensed under the Apache License, Version 2.0 (the "License");
||| you may not use this file except in compliance with the License.
||| You may obtain a copy of the License at
|||
|||     http://www.apache.org/licenses/LICENSE-2.0
|||
||| Unless required by applicable law or agreed to in writing, software
||| distributed under the License is distributed on an "AS IS" BASIS,
||| WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
||| See the License for the specific language governing permissions and
||| limitations under the License.

|||
||| @author Hideki Omatsu
|||

{curl-file-attributes character-encoding = "shift-jis"}


|########################################################################################|
{doc-next
    {purpose  押下連続処理ボタン                                                       }
    {details {itemize
                 {item {code 解説}  ： 
                     押下している間、定期的にイベントを発生させるボタン
                 }
                 {item {code 備考}  ： 特になし。          }
                 {item {code 環境}  ： Surge6.0            }
                 {item {code 新規}  ： 2008/08/31 尾松秀紀 
                     新規作成                              }
                 {item {code 変更}  ：                     }
                 {item {code V/R }  ： 01/01               }
                 {item {code Copyright(c)} SCS 2008        }
                 {item {code Author}： 株式会社ベーシック  }}}
}
|########################################################################################|
{define-class public PressingEventButton {inherits CommandButton}
  
  |###|{doc-next {purpose 周期時間}}|###|
  field private _interval:Time
  
  |###|{doc-next {purpose 実行される処理}}|###|
  field private _timeout-porc:#{proc-type {button:int}:bool}
  
  |###|{doc-next {purpose タイマー}}|###|
  field private _timer:#Timer
  
  |###|{doc-next {purpose ボタン表示するか？}
         {notes
             非表示でも、機能します。
         }
       }
  |###|
  field private _paint?:bool
  
  
  |######################################################################################|
  {doc-next 
    {purpose 押下連続処理ボタンのコンストラクタ                          }
    {details {itemize
                 {item {code 解説}  ： 
                     初期処理を行う。                                    }
                 {item {code 新規}  :  2008/08/31 尾松秀紀               }}}
    {parameter interval, 周期時間                                        }
    {parameter paint?, ボタン表示するか？                                }
  }
  |######################################################################################|
  {constructor public {default
                          interval:Time,
                          paint?:bool = true,
                          ...}
    set self._interval = interval
    set self._paint? = paint?
    {construct-super {splice ...}}
    set self.control-color = {manifest-url "image", "Button"}
    set self.color = control-text-color
  }
  
  
  |######################################################################################|
  {doc-next 
    {purpose 実行する処理を登録                                          }
    {details {itemize
                 {item {code 解説}  ： 
                     実行する処理を登録する。                            }
                 {item {code 新規}  :  2008/08/31 尾松秀紀               }}}
    {parameter proc, 実行する処理                                        }
  }
  |######################################################################################|
  {method public {set-timeout-proc
                     proc:{proc-type {button:int}:bool}
                 }:void
    set self._timeout-porc = proc
  }
  
  
  |######################################################################################|
  {doc-next 
    {purpose ボタン押下時のイベント処理                                  }
    {details {itemize
                 {item {code 解説}  ： 
                     有効であれば、タイマーを定周期で起動し、登録された
                     処理が実行されるようにする。
                 }
                 {item {code 新規}  :  2008/08/31 尾松秀紀               }}}
    {parameter e, PointerPressイベント                                   }
  }
  |######################################################################################|
  {method public {on-pointer-press
                     e:PointerPress}:void
    {if not self.enabled? then  {return}}
    {if-non-null self._timeout-porc then
        {self._timeout-porc e.button}
    }
    set self._timer = {Timer
                          interval = self._interval,
                          {on TimerEvent do
                              {if-non-null self._timeout-porc then
                                  let result:bool = {self._timeout-porc e.button}
                                  {if not result then
                                      {self._timer.disable}
                                  }
                              }
                          }
                      }
    {self._timer.enable}
  }
  
  
  |######################################################################################|
  {doc-next 
    {purpose ボタンを離した時のイベント処理                              }
    {details {itemize
                 {item {code 解説}  ： 
                     有効であれば、タイマーを止める。                    }
                 {item {code 新規}  :  2008/08/31 尾松秀紀               }}}
    {parameter e, PointerReleaseイベント                                 }
  }
  |######################################################################################|
  {method public {on-pointer-release
                     e:PointerRelease}:void
    {if not self.enabled? then  {return}}
    {if-non-null self._timer then
        {self._timer.disable}
    }
  }
  
  
  |######################################################################################|
  {doc-next 
    {purpose ボタンが外に出た時のイベント処理                            }
    {details {itemize
                 {item {code 解説}  ： 
                     有効であれば、タイマーを止める。                    }
                 {item {code 新規}  :  2008/08/31 尾松秀紀               }}}
    {parameter e, PointerReleaseイベント                                 }
    {notes
        PointerReleaseイベントが取得できなかった場合の回避策
    }
  }
  |######################################################################################|
  {method public {on-pointer-leave
                     e:PointerLeave}:void
    {if not self.enabled? then  {return}}
    {if-non-null self._timer then
        {self._timer.disable}
    }
  }
  
  
  |######################################################################################|
  {doc-next 
    {purpose フォーカスアウト時のイベント処理                            }
    {details {itemize
                 {item {code 解説}  ： 
                     有効であれば、タイマーを止める。                    }
                 {item {code 新規}  :  2008/08/31 尾松秀紀               }}}
    {parameter e, PointerReleaseイベント                                 }
    {notes
        PointerReleaseイベントが取得できなかった場合の回避策
    }
  }
  |######################################################################################|
  {method public {on-focus-out
                     e:FocusOut}:void
    {if not self.enabled? then  {return}}
    {if-non-null self._timer then
        {self._timer.disable}
    }
  }
  
  
  |######################################################################################|
  {doc-next 
    {purpose ボタン表示処理                                              }
    {details {itemize
                 {item {code 解説}  ： 
                     ボタンが表示に設定されている場合、ボタンを表示します。
                 }
                 {item {code 新規}  :  2008/08/31 尾松秀紀               }}}
    {parameter ren, Renderer2d                                           }
  }
  |######################################################################################|
  {method public {draw
                     ren:Renderer2d}:void
    {if self._paint? then
        {super.draw ren}
    }
  }
  
  
}
